## 4.2.4 Physical Fields

The compiler assumes that a property is a **physical field** if at leat one of the access identifiers is `default` or `null`. If this is not the case, then the property is considered to be `virtual`.   

> ##### Define: Physical field
>
> A **physical field** means there is an actual variable for data storage.  A field is considered to be **physical** if it is:
> 
> 
> * a [variable](4.1-Variable.md)
> * a [property](4.2-Property.md) with the read-access or write-access identifier being `default` or `null`
> * a [property](4.2-Property.md) with `:isVar` [metadata](7.9-Metadata.md)
> 
> 
> 


If a **virtual** property tries to access its underlying field it will cause a compile-time error, as in this example:  
```haxe
class Main {
	//Compiling fails with the error:
	//this field cannot be accessed because it is not a real variable
	public var x(get, set):Int;
	
	function get_x() {
		return x;
	}
	
	function set_x(x) {
		return this.x = x;
	}
	
	static public function main() {}
}

```

If a physical field is indeed intended, it can be forced by attributing the field in question with the `:isVar` [metadata](7.9-Metadata.md):

```haxe
class Main {
    //@isVar forces the field to be physical 
    //allowing the program to compile.
	@:isVar public var x(get, set):Int;
	
	function get_x() {
		return x;
	}
	
	function set_x(x) {
		return this.x = x;
	}
	
	static public function main() {}
}

```


> ##### Trivia: Property setter type
>
> It is not uncommon for new Haxe users to be surprised by the type of a setter being required to be `T->T` instead of the seemingly more natural `T->Void`. After all, why would a **setter** have to return something?
> 
> Remember, there are no statements in Haxe, only expressions.  We still want to be able to use field assignments using setters as right-side expressions. Given a chain like `x = y = 1`, it is evaluated as `x = (y = 1)`. In order to assign the result of `y = 1` to `x`, the former must have a value. If `y` had a setter returning `Void`, this would not be possible.

---

Previous section: [Rules for getter and setter](4.2.3-Rules_for_getter_and_setter.md)

Next section: [Method](4.3-Method.md)